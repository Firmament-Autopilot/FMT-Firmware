import os
import sys
import rtconfig
import platform
import subprocess
import uuid

if os.path.exists('rt-thread'):
    RTT_ROOT = os.path.normpath(os.getcwd() + '/rt-thread')
else:
    RTT_ROOT = os.path.normpath(os.getcwd() + '/../../rt-thread')

sys.path = sys.path + [os.path.join(RTT_ROOT, 'tools')]
try:
    from building import *
except Exception as e:
    print("Error message:", e)
    print('Cannot found RT-Thread root directory, please check RTT_ROOT')
    print(RTT_ROOT)
    sys.exit(-1)

TARGET = 'rt-thread.' + rtconfig.TARGET_EXT

DefaultEnvironment(tools=[])
env = Environment(tools = ['mingw'], ENV=os.environ,
    AS = rtconfig.AS, ASFLAGS = rtconfig.AFLAGS,
    CC = rtconfig.CC, CFLAGS = rtconfig.CFLAGS,
    AR = rtconfig.AR, ARFLAGS = '-rc',
    CXX = rtconfig.CXX, CXXFLAGS = rtconfig.CXXFLAGS,
    LINK = rtconfig.LINK, LINKFLAGS = rtconfig.LFLAGS)
env.PrependENVPath('PATH', rtconfig.EXEC_PATH)

OBJCOPY = os.path.join(rtconfig.EXEC_PATH, 'arm-none-eabi-objcopy')

if rtconfig.PLATFORM in ['iccarm']:
    env.Replace(CCCOM = ['$CC $CFLAGS $CPPFLAGS $_CPPDEFFLAGS $_CPPINCFLAGS -o $TARGET $SOURCES'])
    env.Replace(ARFLAGS = [''])
    env.Replace(LINKCOM = env["LINKCOM"] + ' --map rt-thread.map')

Export('RTT_ROOT')
Export('rtconfig')

SDK_ROOT = os.path.abspath('./')
libraries_path_prefix = os.path.dirname(SDK_ROOT) + '/libraries'

SDK_LIB = libraries_path_prefix
Export('SDK_LIB')

# prepare building environment
objs = PrepareBuilding(env, RTT_ROOT, has_libcpu=False)

IFX_library = 'Packages'
rtconfig.BSP_LIBRARY_TYPE = IFX_library

# set spawn
def ourspawn(sh, escape, cmd, args, e):
    filename = str(uuid.uuid4())
    newargs = ' '.join(args[1:])
    cmdline = cmd + " " + newargs
    if (len(cmdline) > 16 * 1024):
        f = open(filename, 'w')
        f.write(' '.join(args[1:]).replace('\\', '/'))
        f.close()
        # exec
        cmdline = cmd + " @" + filename
    proc = subprocess.Popen(cmdline, stdin=subprocess.PIPE, stdout=subprocess.PIPE,
        stderr=subprocess.PIPE, shell = False, env = e)
    data, err = proc.communicate()
    rv = proc.wait()
    def res_output(_output, _s):
        if len(_s):
            if isinstance(_s, str):
                _output(_s)
            elif isinstance(_s, bytes):
                _output(str(_s, 'UTF-8'))
            else:
                _output(str(_s))
    res_output(sys.stderr.write, err)
    res_output(sys.stdout.write, data)
    if os.path.isfile(filename):
        os.remove(filename)
    return rv

if platform.system() == 'Windows':
    env['SPAWN'] = ourspawn

env.VariantDir('build/libs', '../libs', duplicate=0)
env.VariantDir('build/libraries', '../libraries', duplicate=0)

objs.extend(SConscript('/board/ports/SConscript', variant_dir='build/board/ports/', duplicate=0))
objs.extend(SConscript('../libraries/Infineon_cmsis-latest/SConscript', variant_dir='build/libraries/Infineon_cmsis-latest', duplicate=0))
objs.extend(SConscript('../libraries/Infineon_core-lib-latest/SConscript', variant_dir='build/libraries/Infineon_core-lib-latest', duplicate=0))
objs.extend(SConscript('../libraries/mtb-device-support-pse8xxgp/hal/SConscript', variant_dir='build/libraries/mtb-device-support-pse8xxgp/hal', duplicate=0))
objs.extend(SConscript('../libraries/mtb-device-support-pse8xxgp/pdl/SConscript', variant_dir='build/libraries/mtb-device-support-pse8xxgp/pdl', duplicate=0))
objs.extend(SConscript('../libraries/async-transfer/SConscript', variant_dir='build/libraries/async-transfer', duplicate=0))
objs.extend(SConscript('../libraries/mtb-device-support-pse8xxgp/device-utils/syspm/SConscript', variant_dir='build/libraries/mtb-device-support-pse8xxgp/device-utils/syspm', duplicate=0))
objs.extend(SConscript('../libraries/serial-memory/SConscript', variant_dir='build/libraries/serial-memory', duplicate=0))
objs.extend(SConscript('../libraries/Infineon_retarget-io-latest/SConscript', variant_dir='build/libraries/Infineon_retarget-io-latest', duplicate=0))
objs.extend(SConscript('../libraries/SConscript', variant_dir='build/libraries', duplicate=0))
objs.extend(SConscript('../libraries/mtb-srf/SConscript', variant_dir='build/libraries/mtb-srf', duplicate=0))
objs.extend(SConscript('../libraries/mtb-ipc/SConscript', variant_dir='build/libraries/mtb-ipc', duplicate=0))
objs.extend(SConscript('../libraries/littlefs/SConscript', variant_dir='build/libraries/littlefs', duplicate=0))


# make a building
DoBuilding(TARGET, objs)

# Generate HEX file
DEBUG_DIR = 'build'
if not os.path.exists(DEBUG_DIR):
    os.makedirs(DEBUG_DIR)
hex_file = env.Command(DEBUG_DIR + '/rtthread.hex', TARGET, OBJCOPY + ' -O ihex $SOURCE $TARGET')

# SDK_ROOT is the project directory, go up 2 levels to reach sdk-bsp root
SDK_BSP_ROOT = os.path.dirname(os.path.dirname(SDK_ROOT + '../'))
EDGEPROTECTTOOLS = os.path.join(SDK_BSP_ROOT, 'tools', 'edgeprotecttools', 'bin', 'edgeprotecttools.exe')
BOOT_CONFIG = os.path.join(SDK_ROOT, 'config', 'boot_with_extended_boot_scons.json')

if os.path.exists(EDGEPROTECTTOOLS) and os.path.exists(BOOT_CONFIG):
    secure_image = env.Command(
        'secure_image',  # Pseudo target
        hex_file,
        '"' + EDGEPROTECTTOOLS + '" run-config -i "' + BOOT_CONFIG + '"'
    )
else:
    print("edgeprotecttools or boot config not found, skipping secure image packaging")
    print("EDGEPROTECTTOOLS:", EDGEPROTECTTOOLS)
    print("BOOT_CONFIG:", BOOT_CONFIG)
